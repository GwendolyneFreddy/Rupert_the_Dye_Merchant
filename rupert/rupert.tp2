BACKUP ~rupert/backup~
//AUTHOR ~jonalun@gmail.com~
SUPPORT ~Spellhold Studios at http://www.shsforums.net/topic/24786-rupert-the-dye-merchant-v300/~

VERSION ~v3.0.0~

README ~rupert/readme/rupert-readme-english.html~

ALWAYS

	ACTION_IF NOT VARIABLE_IS_SET is_tutu BEGIN // check to make this happen only once per install

	/* -------------------------- *
	 *    Sod: Modmerge check.    *
	 * -------------------------- */
		ACTION_IF ((FILE_EXISTS ~dlc/sod-dlc.zip~) OR (FILE_EXISTS ~sod-dlc.zip~)) BEGIN FAIL @21 END

	/* -------------------------------------------------------------------------------- *
	 *    Determines which game is installed and if the game is an Enhanced Edition.    *
	 * -------------------------------------------------------------------------------- */
		OUTER_SET is_tutu = (GAME_IS ~tutu tutu_totsc~ ? 1 : 0)						// tutu
		OUTER_SET is_bgt = (GAME_IS ~bgt~) ? 1 : 0									// bgt

		OUTER_SET is_bgee = (GAME_IS ~bgee~) ? 1 : 0								// bgee
		OUTER_SET is_bg2ee = (GAME_IS "bg2ee") ? 1 : 0								// b2gee
		OUTER_SET is_eet = (GAME_IS "eet") ? 1 : 0									// eet
		OUTER_SET is_ee = (GAME_IS ~bgee bg2ee eet~) ? 1 : 0						// EE game

		OUTER_SET is_bg2 = (GAME_IS ~bg2~) ? 1 : 0									// bg2
		OUTER_SET is_tob = (GAME_IS ~tob bgt bg2ee eet~) ? 1 : 0					// tob

	/* --------------------------------------------------------------------------- *
	 *    Defines Crossplatform variables according to which game is installed.    *
	 * --------------------------------------------------------------------------- */
		OUTER_SPRINT "FriendlyArmInn_L2" "none"
		OUTER_SPRINT "FriendlyArmInn_L2_BCS" "none"
		OUTER_SPRINT "MithrestInn" "AR0704"
		OUTER_SPRINT "SaradushTavern" "none"
		ACTION_IF is_tutu BEGIN
			OUTER_SPRINT "FriendlyArmInn_L2" "FW2302"
			OUTER_SPRINT "FriendlyArmInn_L2_BCS" "_AR2302"
			OUTER_SPRINT "MithrestInn" "none"
			PRINT @22	// ~Tutu install detected.~
		END
		ACTION_IF is_bgt BEGIN
			OUTER_SPRINT "FriendlyArmInn_L2" "AR6802"
			OUTER_SPRINT "FriendlyArmInn_L2_BCS" "AR6802"
			PRINT @23	// ~BGT install detected.~
		END
		ACTION_IF is_bgee BEGIN
			OUTER_SPRINT "FriendlyArmInn_L2" "AR2302"
			OUTER_SPRINT "FriendlyArmInn_L2_BCS" "AR2302"
			OUTER_SPRINT "MithrestInn" "none"
			PRINT @24	// ~Baldur's Gate Enhanced Edition install detected.~
		END
		ACTION_IF is_eet BEGIN
			OUTER_SPRINT "FriendlyArmInn_L2" "BG2302"
			OUTER_SPRINT "FriendlyArmInn_L2_BCS" "BG2302"
			PRINT @25	// ~Enhanced Edition Trilogy install detected.~
		END
		ACTION_IF is_tob BEGIN
			OUTER_SPRINT "SaradushTavern" "AR5003"
			PRINT @26	// ~Throne of Bhaal install detected.~
		END

	/* --------------------------------------------------------------------------------------------- *
	 *    Convert strings to UTF-8 for BGEE/BG2EE                                                    *
	 *    No exception, everything needs to be converted                                             *
	 *    setup.tra contains in game texts as well as installation texts and needs to be converted   *
	 * --------------------------------------------------------------------------------------------- */
		ACTION_DEFINE_ARRAY jl#noconvert BEGIN END
		// any tra files used in LANGUAGE need to be reloaded after UTF-8 conversion
		ACTION_DEFINE_ARRAY jl#reload BEGIN setup END

		LAF HANDLE_CHARSETS
			INT_VAR
				infer_charsets = 1
			STR_VAR
				tra_path = EVAL ~%MOD_FOLDER%/lang~
				noconvert_array = jl#noconvert
				reload_array = jl#reload
		END

	END

END // of ALWAYS block


AUTO_TRA ~rupert/lang/%s~

LANGUAGE ~English~
         ~english~
         ~rupert/lang/english/setup.tra~

LANGUAGE ~Deutsch (Uebersetzung von Meresin)~
         ~german~
         ~rupert/lang/german/setup.tra~

LANGUAGE ~Espanol (traduccion por Lisandro)~
         ~spanish~
         ~rupert/lang/spanish/setup.tra~

LANGUAGE ~Italiano (traduzione di ilot)~
         ~italian~
         ~rupert/lang/italian/setup.tra~

LANGUAGE ~Francais (Traduction : Gwendolyne)~
         ~french~
         ~rupert/lang/french/setup.tra~

LANGUAGE ~Russian (Translation by the BWP Russian Textpack team)~
         ~russian~
         ~rupert/lang/russian/setup.tra~


/* ============================= *
 *    Rupert the Dye Merchant    *
 * ============================= */
BEGIN @0
DESIGNATED 0 LABEL "rupert_the_dye_merchant"
REQUIRE_PREDICATE GAME_IS ~bg2 tob tutu tutu_totsc bgt bgee bg2ee eet~ @101	// ~You must have BG2, ToB, Tutu, BGT, BG:EE, SoD, BG2:EE or EET installed to install Rupert the Dye Merchant mod.~

/* --------------------------------- *
 *  Copy BAMs or convert them to EE  *
 * --------------------------------- */
ACTION_IF is_ee BEGIN
	INCLUDE ~%MOD_FOLDER%/lib/gw_convert_inventory_bam_to_ee.tpa~	// Loads function converting inventory BAMs to EE.
	OUTER_SPRINT file-exc ~jl#ccclr jl#ccfgt jl#ccmag jl#ccthf ~	// Special case: description icons don't need to be converted.
	ACTION_BASH_FOR ~%MOD_FOLDER%/bams/~ ~^.+\.bam$~ BEGIN
		ACTION_TO_LOWER %BASH_FOR_FILE%
		ACTION_TO_LOWER %BASH_FOR_RES%
		ACTION_IF (~%file-exc%~ STRING_CONTAINS_REGEXP ~%BASH_FOR_RES%~ = 1) BEGIN
			COPY ~%BASH_FOR_FILESPEC%~ ~override/%BASH_FOR_FILE%~
				LPF ~gw_convert_inventory_bam_to_ee~ END
			BUT_ONLY
		END ELSE BEGIN
			COPY ~%BASH_FOR_FILESPEC%~ ~override/%BASH_FOR_FILE%~
		END
	END
END ELSE BEGIN
	COPY ~%MOD_FOLDER%/bams~	~override~
END

/* ------------------ *
 *  Installing items  *
 * ------------------ */

COPY ~rupert/itm/large~ ~override~
	SAY NAME1 @101
	SAY NAME2 @102
	SAY DESC @103

COPY ~rupert/itm/small~ ~override~
	SAY NAME1 @104
	SAY NAME2 @105
	SAY DESC @106

COPY ~rupert/itm/leather~ ~override~
	SAY NAME1 @107
	SAY NAME2 @108
	SAY DESC @109

COPY ~rupert/itm/armour~ ~override~
	SAY NAME1 @110
	SAY NAME2 @111
	SAY DESC @112

COPY ~rupert/itm/buckle~ ~override~
	SAY NAME1 @113
	SAY NAME2 @114
	SAY DESC @115

COPY ~rupert/itm/hair~ ~override~
	SAY NAME1 @116
	SAY NAME2 @117
	SAY DESC @118

COPY ~rupert/itm/clothes/jl#cmage.itm~ ~override~
	SAY NAME1 @128
	SAY NAME2 @128
	SAY DESC @129

COPY ~rupert/itm/clothes/jl#cfgtr.itm~ ~override~
	SAY NAME1 @130
	SAY NAME2 @130
	SAY DESC @131

COPY ~rupert/itm/clothes/jl#cclrc.itm~ ~override~
	SAY NAME1 @132
	SAY NAME2 @132
	SAY DESC @133

COPY ~rupert/itm/clothes/jl#cthif.itm~ ~override~
	SAY NAME1 @134
	SAY NAME2 @134
	SAY DESC @135

COMPILE ~rupert/baf~

COPY ~rupert/eff~ ~override~

COPY ~rupert/cre/cutspy.cre~ ~override/jl#rcclr.cre~
	WRITE_ASCII 0x248 ~JL#RCCLR~ #8

COPY ~rupert/cre/cutspy.cre~ ~override/jl#rcftr.cre~
	WRITE_ASCII 0x248 ~JL#RCFTR~ #8

COPY ~rupert/cre/cutspy.cre~ ~override/jl#rcmag.cre~
	WRITE_ASCII 0x248 ~JL#RCMAG~ #8

COPY ~rupert/cre/cutspy.cre~ ~override/jl#rcthf.cre~
	WRITE_ASCII 0x248 ~JL#RCTHF~ #8


/* ----------------------------------- *
 *  Installing games specific content  *
 * ----------------------------------- */

// BG content (Friendly Arm Inn Level2)
// ------------------------------------
ACTION_IF ("%FriendlyArmInn_L2%" STR_CMP "none") BEGIN
	COPY ~rupert/cre/jl#ruprt.cre~ ~override~
	SAY NAME1 @119
	SAY NAME2 @119
	COMPILE ~rupert/dlg/jl#ruprt.d~
	COPY_EXISTING ~%FriendlyArmInn_L2%.are~ ~override~
		READ_ASCII AREA_SCRIPT script (8)
		// if it doesn't exist, give it a default name
		PATCH_IF ("%script%" STRING_EQUAL_CASE ~~ OR "%script%" STRING_EQUAL_CASE ~none~ OR !FILE_EXISTS_IN_GAME ~%script%.bcs~) BEGIN
			WRITE_ASCIIE AREA_SCRIPT ~%FriendlyArmInn_L2_BCS%~ #8
		END
	BUT_ONLY
	EXTEND_TOP ~%FriendlyArmInn_L2_BCS%.bcs~ ~rupert/script/jl#ruprt.baf~ EVALUATE_BUFFER
END

// BG2 content (Mithrest Inn)
// --------------------------
ACTION_IF ("%MithrestInn%" STR_CMP "none") BEGIN
		COPY ~rupert/cre/jl#ruprt.cre~ ~override/jl#rupr2.cre~
		SAY NAME1 @119
		SAY NAME2 @119
		WRITE_ASCII DIALOG ~JL#RUPR2~
		COMPILE ~rupert/dlg/jl#rupr2.d~
		EXTEND_TOP ~%MithrestInn%.bcs~ ~rupert/script/jl#rupr2.baf~
		EXTEND_TOP ~edwin.bcs~ ~rupert/script/edwin.baf~ // Check if Edwin was a girl
END

// ToB content (Saradush Tavern - Tankard Tree and Amkethran Inn)
// --------------------------------------------------------------
ACTION_IF ("%SaradushTavern%" STR_CMP "none") BEGIN
		COPY ~rupert/cre/jl#ruprt.cre~ ~override/jl#rupr3.cre~
			SAY NAME1 @119
			SAY NAME2 @119
			WRITE_ASCII DIALOG ~JL#RUPR3~
		COMPILE ~rupert/dlg/jl#rupr3.d~
		EXTEND_TOP ~%SaradushTavern%.bcs~ ~rupert/script/jl#rupr3.baf~

		COPY ~rupert/cre/jl#rupr4.cre~ ~override~
			SAY NAME1 @120
			SAY NAME2 @120
			WRITE_ASCII DIALOG ~JL#RUPR4~
		COMPILE ~rupert/dlg/jl#rupr4.d~
		COPY_EXISTING ~ar5501.are~ ~override~
			READ_ASCII AREA_SCRIPT script (8)
			// if it doesn't exist, give it a default name
			PATCH_IF ("%script%" STRING_EQUAL_CASE ~~ OR "%script%" STRING_EQUAL_CASE ~none~ OR !FILE_EXISTS_IN_GAME ~%script%.bcs~) BEGIN
				WRITE_ASCIIE AREA_SCRIPT ~AR5501~ #8
			END
		BUT_ONLY
		EXTEND_TOP ~ar5501.bcs~ ~rupert/script/jl#rupr4.baf~
END


/* ------------------- *
 *  Installing stores  *
 * ------------------- */
// Rupert's Dyes - Cloth (large)
COPY ~rupert/sto/jl#ruprt.sto~ ~override/jl#rup_a.sto~
	SAY STORE_NAME @121
	ADD_STORE_ITEM ~JL#PA000~ #1 #0 #0 ~IDENTIFIED~ #1 ~UNLIMITED~
	FOR (i = 1 ; i < 116 ; ++i) BEGIN
		SET previous = i - 1
		PATCH_IF (i < 10) BEGIN
			SPRINT dye EVAL "JL#PA00%i%"
		END ELSE BEGIN
			PATCH_IF (i < 100) BEGIN
				SPRINT dye EVAL "JL#PA0%i%"
			END ELSE BEGIN
				SPRINT dye EVAL "JL#PA%i%"
			END
		END
		PATCH_IF (previous < 10) BEGIN
			SPRINT dye_bef EVAL "JL#PA00%previous%"
		END ELSE BEGIN
			PATCH_IF (previous < 100) BEGIN
				SPRINT dye_bef EVAL "JL#PA0%previous%"
			END ELSE BEGIN
				SPRINT dye_bef EVAL "JL#PA%previous%"
			END
		END
		ADD_STORE_ITEM ~%dye%~ AFTER ~%dye_bef%~ #1 #0 #0 ~IDENTIFIED~ #1 ~UNLIMITED~
	END
BUT_ONLY

// Rupert's Dyes - Cloth (small)
COPY ~rupert/sto/jl#ruprt.sto~ ~override/jl#rup_b.sto~
	SAY STORE_NAME @122
	ADD_STORE_ITEM ~JL#PB000~ #1 #0 #0 ~IDENTIFIED~ #1 ~UNLIMITED~
	FOR (i = 1 ; i < 116 ; ++i) BEGIN
		SET previous = i - 1
		PATCH_IF (i < 10) BEGIN
			SPRINT dye EVAL "JL#PB00%i%"
		END ELSE BEGIN
			PATCH_IF (i < 100) BEGIN
				SPRINT dye EVAL "JL#PB0%i%"
			END ELSE BEGIN
				SPRINT dye EVAL "JL#PB%i%"
			END
		END
		PATCH_IF (previous < 10) BEGIN
			SPRINT dye_bef EVAL "JL#PB00%previous%"
		END ELSE BEGIN
			PATCH_IF (previous < 100) BEGIN
				SPRINT dye_bef EVAL "JL#PB0%previous%"
			END ELSE BEGIN
				SPRINT dye_bef EVAL "JL#PB%previous%"
			END
		END
		ADD_STORE_ITEM ~%dye%~ AFTER ~%dye_bef%~ #1 #0 #0 ~IDENTIFIED~ #1 ~UNLIMITED~
	END
BUT_ONLY

// Rupert's Dyes - Leather
COPY ~rupert/sto/jl#ruprt.sto~ ~override/jl#rup_c.sto~
	SAY STORE_NAME @123
	ADD_STORE_ITEM ~JL#PC000~ #1 #0 #0 ~IDENTIFIED~ #1 ~UNLIMITED~
	FOR (i = 1 ; i < 116 ; ++i) BEGIN
		SET previous = i - 1
		PATCH_IF (i < 10) BEGIN
			SPRINT dye EVAL "JL#PC00%i%"
		END ELSE BEGIN
			PATCH_IF (i < 100) BEGIN
				SPRINT dye EVAL "JL#PC0%i%"
			END ELSE BEGIN
				SPRINT dye EVAL "JL#PC%i%"
			END
		END
		PATCH_IF (previous < 10) BEGIN
			SPRINT dye_bef EVAL "JL#PC00%previous%"
		END ELSE BEGIN
			PATCH_IF (previous < 100) BEGIN
				SPRINT dye_bef EVAL "JL#PC0%previous%"
			END ELSE BEGIN
				SPRINT dye_bef EVAL "JL#PC%previous%"
			END
		END
		ADD_STORE_ITEM ~%dye%~ AFTER ~%dye_bef%~ #1 #0 #0 ~IDENTIFIED~ #1 ~UNLIMITED~
	END
BUT_ONLY

// Rupert's Dyes - Armour
COPY ~rupert/sto/jl#ruprt.sto~ ~override/jl#rup_d.sto~
	SAY STORE_NAME @124
	ADD_STORE_ITEM ~JL#PD000~ #1 #0 #0 ~IDENTIFIED~ #1 ~UNLIMITED~
	FOR (i = 1 ; i < 116 ; ++i) BEGIN
		SET previous = i - 1
		PATCH_IF (i < 10) BEGIN
			SPRINT dye EVAL "JL#PD00%i%"
		END ELSE BEGIN
			PATCH_IF (i < 100) BEGIN
				SPRINT dye EVAL "JL#PD0%i%"
			END ELSE BEGIN
				SPRINT dye EVAL "JL#PD%i%"
			END
		END
		PATCH_IF (previous < 10) BEGIN
			SPRINT dye_bef EVAL "JL#PD00%previous%"
		END ELSE BEGIN
			PATCH_IF (previous < 100) BEGIN
				SPRINT dye_bef EVAL "JL#PD0%previous%"
			END ELSE BEGIN
				SPRINT dye_bef EVAL "JL#PD%previous%"
			END
		END
		ADD_STORE_ITEM ~%dye%~ AFTER ~%dye_bef%~ #1 #0 #0 ~IDENTIFIED~ #1 ~UNLIMITED~
	END
BUT_ONLY
BUT_ONLY

// Rupert's Dyes - Details
COPY ~rupert/sto/jl#ruprt.sto~ ~override/jl#rup_e.sto~
	SAY STORE_NAME @125
	ADD_STORE_ITEM ~JL#PE000~ #1 #0 #0 ~IDENTIFIED~ #1 ~UNLIMITED~
	FOR (i = 1 ; i < 116 ; ++i) BEGIN
		SET previous = i - 1
		PATCH_IF (i < 10) BEGIN
			SPRINT dye EVAL "JL#PE00%i%"
		END ELSE BEGIN
			PATCH_IF (i < 100) BEGIN
				SPRINT dye EVAL "JL#PE0%i%"
			END ELSE BEGIN
				SPRINT dye EVAL "JL#PE%i%"
			END
		END
		PATCH_IF (previous < 10) BEGIN
			SPRINT dye_bef EVAL "JL#PE00%previous%"
		END ELSE BEGIN
			PATCH_IF (previous < 100) BEGIN
				SPRINT dye_bef EVAL "JL#PE0%previous%"
			END ELSE BEGIN
				SPRINT dye_bef EVAL "JL#PE%previous%"
			END
		END
		ADD_STORE_ITEM ~%dye%~ AFTER ~%dye_bef%~ #1 #0 #0 ~IDENTIFIED~ #1 ~UNLIMITED~
	END
BUT_ONLY

// Rupert's Dyes - Hair
COPY ~rupert/sto/Jl#ruprt.sto~ ~override/jl#rup_f.sto~
	SAY STORE_NAME @126
	ADD_STORE_ITEM ~JL#PF000~ #1 #0 #0 ~IDENTIFIED~ #1 ~UNLIMITED~
	FOR (i = 1 ; i < 116 ; ++i) BEGIN
		SET previous = i - 1
		PATCH_IF (i < 10) BEGIN
			SPRINT dye EVAL "JL#PF00%i%"
		END ELSE BEGIN
			PATCH_IF (i < 100) BEGIN
				SPRINT dye EVAL "JL#PF0%i%"
			END ELSE BEGIN
				SPRINT dye EVAL "JL#PF%i%"
			END
		END
		PATCH_IF (previous < 10) BEGIN
			SPRINT dye_bef EVAL "JL#PF00%previous%"
		END ELSE BEGIN
			PATCH_IF (previous < 100) BEGIN
				SPRINT dye_bef EVAL "JL#PF0%previous%"
			END ELSE BEGIN
				SPRINT dye_bef EVAL "JL#PF%previous%"
			END
		END
		ADD_STORE_ITEM ~%dye%~ AFTER ~%dye_bef%~ #1 #0 #0 ~IDENTIFIED~ #1 ~UNLIMITED~
	END
BUT_ONLY

// Rupert's Outfits
COPY ~rupert/sto/jl#ruprt.sto~ ~override/jl#rup_g.sto~
	SAY STORE_NAME @127
	ADD_STORE_ITEM ~JL#CTHIF~ #1 #0 #0 ~IDENTIFIED~ #1 ~UNLIMITED~
	ADD_STORE_ITEM ~JL#CFGTR~ #1 #0 #0 ~IDENTIFIED~ #1 ~UNLIMITED~
	ADD_STORE_ITEM ~JL#CCLRC~ #1 #0 #0 ~IDENTIFIED~ #1 ~UNLIMITED~
	ADD_STORE_ITEM ~JL#CMAGE~ #1 #0 #0 ~IDENTIFIED~ #1 ~UNLIMITED~
BUT_ONLY
